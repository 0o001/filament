apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    buildToolsVersion versions.buildTools
    compileSdkVersion versions.compileSdk
    ndkVersion versions.ndk

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk

        externalNativeBuild {
            cmake {
                arguments.addAll(rootProject.ext.cmakeArgs)
                cppFlags.addAll(rootProject.ext.cppFlags)
            }
        }
    }

    flavorDimensions "functionality"
    productFlavors {
        full {
            dimension "functionality"
        }

        lite {
            dimension "functionality"
        }
    }

    externalNativeBuild {
        cmake {
            path "CMakeLists.txt"
        }
    }

    sourceSets {
        main {
            jni.srcDirs "src/main/cpp"
            kotlin.srcDirs += "src/main/java"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // No need to package up the following shared libs, which arise as a side effect of our
    // externalNativeBuild dependencies. When clients pick and choose from project-level gradle
    // dependencies, these shared libs already get pulled in, so we need to avoid the error:
    // "More than one file was found with OS independent path ..."
    packagingOptions {
        exclude 'lib/*/libfilament-jni.so'
        exclude 'lib/*/libgltfio-jni.so'
    }
}

configurations.all { config ->
    // Hack to preserve the version of the dependencies
    if (!config.name.endsWith('Publication')) {
        resolutionStrategy {
            dependencySubstitution {
                substitute(module("com.google.android.filament:gltfio-android:${VERSION_NAME}")).with(project(":gltfio-android"))
                substitute(module("com.google.android.filament:gltfio-android-lite:${VERSION_NAME}")).with(project(":gltfio-android"))
            }
        }
    }
}

dependencies {
    implementation deps.kotlin
    implementation deps.androidx.annotations
    implementation project(':filament-android')

    fullImplementation module("com.google.android.filament:gltfio-android:${VERSION_NAME}")
    liteImplementation module("com.google.android.filament:gltfio-android-lite:${VERSION_NAME}")
}

apply from: rootProject.file('gradle/gradle-mvn-push.gradle')

afterEvaluate {
    publishing {
        publications {
            fullRelease(MavenPublication) {
                artifactId = POM_ARTIFACT_ID_FULL
                from components.fullRelease
            }

            liteRelease(MavenPublication) {
                artifactId = POM_ARTIFACT_ID_LITE
                from components.liteRelease
            }
        }
    }
}
